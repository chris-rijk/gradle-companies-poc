group 'gradle-companies-poc'

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'idea'
    version '1.0'
    sourceCompatibility = 1.8
    repositories {
        jcenter()
    }
}

ext.versions = [
    jersey: "2.26",
    jackson: "2.9.1",
    mockito: "2.8.47",
    junit: "4.12",
    jax_rs: "2.1",
    datanucleus: "5.1.2"
]

ext.libraries = [
    junit: "junit:junit:${versions.junit}",
    mockito: "org.mockito:mockito-core:${versions.mockito}",
    jax_rs: "javax.ws.rs:javax.ws.rs-api:${versions.jax_rs}",
    jwt: "io.jsonwebtoken:jjwt:0.7.0",
    swagger: "io.swagger:swagger-jersey2-jaxrs:1.5.16",
    jersey: [
        "javax.servlet:javax.servlet-api:4.0.0", // required for swagger at runtime
        "org.glassfish.jersey.containers:jersey-container-grizzly2-http:${versions.jersey}",
        "org.glassfish.jersey.inject:jersey-hk2:${versions.jersey}",
        "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:${versions.jackson}",
        "com.fasterxml.jackson.module:jackson-module-parameter-names:${versions.jackson}",
        "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${versions.jackson}",
        "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${versions.jackson}",
    ],
    jersey_test: [
        "org.glassfish.jersey.ext:jersey-proxy-client:${versions.jersey}",
        "org.glassfish.jersey.test-framework:jersey-test-framework-util:${versions.jersey}",
        "org.glassfish.jersey.test-framework:jersey-test-framework-core:${versions.jersey}",
        "org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:${versions.jersey}"
    ],
    jdo: [
        "org.datanucleus:javax.jdo:3.2.0-m7",
        "org.datanucleus:datanucleus-core:${versions.datanucleus}",
        "org.datanucleus:datanucleus-api-jdo:${versions.datanucleus}",
        "org.datanucleus:datanucleus-rdbms:${versions.datanucleus}",
        "com.microsoft.sqlserver:sqljdbc4:4.0"
    ]
]

subprojects {
    buildscript {
        repositories {
            jcenter()
        }
        dependencies {
            classpath 'org.sgornostal:gradle-docker-plugin:1.5.3'
        }
    }

    jacocoTestReport {
        additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
        sourceDirectories = files(sourceSets.main.allSource.srcDirs)
        classDirectories =  files(sourceSets.main.output)
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.25
                }
            }
        }
    }
    
    task datanucleusEnhance(type: JavaExec, group: 'Build', dependsOn: processResources) {
        classpath = project.configurations.compileClasspath + files("$buildDir/classes/java/main")
        main = 'org.datanucleus.enhancer.DataNucleusEnhancer'
        args = fileTree(dir: "$buildDir/classes/java/main", include: '**/*.class')
        doLast {
            String path = "$buildDir/classes/java/main"
            def entityFiles = fileTree(dir: path, include: '**/*.class')
            
            println "Enhancing with DataNucleus the following files in '$path'"
            entityFiles.getFiles().each {
                println it
            }
            println "list complete."
        }
    }
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories =  files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
    onlyIf = {
        true
    }
    doFirst {
        executionData = files(executionData.findAll {
                it.exists()
            })
    }
}